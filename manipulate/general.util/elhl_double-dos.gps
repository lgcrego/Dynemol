set term pngcairo dashed enhanced truecolor font "Helvetica,36" size 1400,1000

set border lw 2

##########################################
# Adjust these ranges to your needs:
#
set yrange  [-15:-5]  # energy
set xrange  [0:0.5]   # time
set cbrange [5e-2:1]  # occupation
set zrange  [5e-2:1]  # not plotted if outside this range (transparent)
##########################################

# x margins
x0 = 0.14
x1 = 0.80
Lx = x1 - x0

# y margin positions
y0 = 0.13
y1 = 0.98
Ly = y1 - y0

set bmargin at screen y0;  set tmargin at screen y1

set output "elhl_NADyn.png"

set multiplot

set lmargin at screen x0;  set rmargin at screen x1

set ytics out offset 1.1, 0.0 nomirror scale 0.5
set xtics out offset 0.0, 0.8 nomirror scale 0.5

set ylabel "Energy (eV)" offset 0.2, 0.0
set xlabel "Time (ps)"   offset 0.0, 1.2

#set cbtics offset -0.2,0

set pm3d map corners2color c1

# great palletes can be found in http://slpr.sakura.ne.jp/qp/pm3d-colormap/

# Electron ----------------------------------------
set palette defined(0"#7f0000",1"#ee0000",2"#ff7000",3"#ffee00")
unset colorbox
#set colorbox user origin x1+0.01,y0+0.01 size 0.035,Ly*0.97
#set format cb ""
splot "Occup_smear.dat" using 1:2:3 notitle

# Hole    ----------------------------------------
set palette defined(0"#000090",1"#0000ff",2"#1e90ff",3"#00ffff")
unset colorbox
#set colorbox user origin x1+0.055,y0+0.01 size 0.035,Ly*0.97
#set format cb "%4.1f"
splot "Occup_smear.dat" using 1:2:4 notitle

unset xtics 
unset xlabel
unset ytics 
unset ylabel
plot "Occup_el.dat" using 1:3 w p pt 7 ps 0.001 lc rgb 'grey30' notitle 
unset multiplot
